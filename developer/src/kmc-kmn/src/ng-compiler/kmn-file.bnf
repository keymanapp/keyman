kmnTree: line*
line: contentLine|blankLine
contentLine: WHITESPACE? content WHITESPACE? COMMENT? NEWLINE
blankLine: WHITESPACE? COMMENT? NEWLINE
padding: continuationNewline|WHITESPACE
continuationNewline: WHITESPACE? CONTINUATION WHITESPACE? NEWLINE WHITESPACE?
content: stringSystemStoreAssign|casedkeysStoreAssign|hotkeyStoreAssign|
  capsAlwaysOff|capsOnOnly|shiftFreesCaps|variableStoreAssign|ruleBlock
prePadText: padding text
text: plainText|outsStatement
plainText: textRange|simpleText
simpleText: STRING|virtualKey|U_CHAR
textRange: simpleText rangeEnd+
rangeEnd: WHITESPACE? RANGE WHITESPACE? simpleText
virtualKey: LEFT_SQ WHITESPACE? spacedModifier* KEY_CODE WHITESPACE? RIGHT_SQ
spacedModifier: modifier WHITESPACE
modifier: SHIFT|CAPS|MODIFIER
outsStatement: OUTS bracketedStoreName
ruleBlock: beginBlock|groupBlock|usingKeysProductionBlock|readOnlyProductionBlock|
  contextProductionBlock
beginBlock: beginStatement spacedChevron useStatement
beginStatement: BEGIN spacedEntryPoint?
spacedEntryPoint: WHITESPACE entryPoint
entryPoint: UNICODE|NEWCONTEXT|POSTKEYSTROKE|ANSI
spacedChevron: WHITESPACE CHEVRON WHITESPACE
useStatement: USE bracketedGroupName
bracketedGroupName: LEFT_BR WHITESPACE? groupNameOrKeyword WHITESPACE? RIGHT_BR
groupNameOrKeyword: groupName|permittedKeyword
groupName: PARAMETER
groupBlock: groupStatement spacedGroupQualifier?
groupStatement: GROUP bracketedGroupName
spacedGroupQualifier: WHITESPACE groupQualifier
groupQualifier: usingKeys|READONLY
usingKeys: USING WHITESPACE KEYS
usingKeysProductionBlock: usingKeysLhsBlock spacedChevron rhsBlock
readOnlyProductionBlock: readOnlyLhsBlock spacedChevron rhsBlock
contextProductionBlock: contextLhsBlock spacedChevron rhsBlock
usingKeysLhsBlock: usingKeysInputBlock
readOnlyLhsBlock: MATCH|NOMATCH|readOnlyInputBlock|ifLikeBlock
contextLhsBlock: contextInputBlock|nulInputBlock
usingKeysInputBlock: postPadNul? postPadIfLikeBlock? postPadInputContext? PLUS padding keystroke
readOnlyInputBlock: postPadNul? postPadIfLikeBlock? keystroke
contextInputBlock: postPadNul? postPadIfLikeBlock? inputContext
nulInputBlock: NUL prePadIfLikeBlock?
postPadNul: NUL padding
postPadInputContext: inputContext padding
inputContext: inputElement prePadInputElement*
prePadInputElement: padding inputElement
inputElement: anyStatement|notAnyStatement|deadKeyStatement|contextStatement|text
keystroke: anyStatement|text
anyStatement: ANY bracketedStoreName
notAnyStatement: NOTANY bracketedStoreName
prePadIfLikeBlock: padding IfLikeBlock
postPadIfLikeBlock: ifLikeBlock padding
ifLikeBlock: ifLikeStatement prePadIfLikeStatement*
prePadIfLikeStatement: padding IfLikeStatement
ifLikeStatement: ifStatement|platformStatement|baselayoutStatement
ifStatement: ifStoreStringStatement|ifSystemStoreStringStatement
ifStoreStringStatement: IF LEFT_BR WHITESPACE? variableStoreName WHITESPACE
  comparison WHITESPACE STRING WHITESPACE? RIGHT_BR
ifSystemStoreStringStatement: IF LEFT_BR WHITESPACE? AMPERSAND systemStoreName
  WHITESPACE comparison WHITESPACE STRING WHITESPACE? RIGHT_BR
ifSystemStoreName: PLATFORM|LAYER|BASELAYOUT|NEWLAYER|OLDLAYER
comparison:EQUAL|NOT_EQUAL
layerStatement: LAYER bracketedString
platformStatement: PLATFORM bracketedString
baselayoutStatement: BASELAYOUT bracketedString
bracketedString: LEFT_BR WHITESPACE? STRING WHITESPACE? RIGHT_BR
rhsBlock: contextOutputBlock|CONTEXT|RETURN|NUL
contextOutputBlock: postPadContext? outputBlock
postPadContext: CONTEXT padding
outputBlock: outputStatement prePadOutputStatement*
prePadOutputStatement: padding outputStatement
outputStatement: useStatement|callStatement|setStore|saveStatement|
  resetStore|deadKeyStatement|setLayer|layerStatement|indexStatement|
  contextStatement|text|BEEP
callStatement: CALL bracketedStoreName
saveStatement: SAVE bracketedStoreName
deadKeyStatement: DEADKEY bracketedStoreName
indexStatement: INDEX LEFT_BR WHITESPACE? variableStoreName spacedComma offset WHITESPACE? RIGHT_BR
spacedComma: WHITESPACE? COMMA WHITESPACE?
offset: PARAMETER
contextStatement: CONTEXT LEFT_BR WHITESPACE? offset WHITESPACE? RIGHT_BR

# System and Variable Store Rules
stringSystemStoreAssign: stringSystemStore padding STRING
stringSystemStore: STORE LEFT_BR WHITESPACE? AMPERSAND stringSystemStoreName WHITESPACE? RIGHT_BR
stringSystemStoreName: BITMAP|COPYRIGHT|DISPLAYMAP|ETHNOLOGUECODE|INCLUDECODES|
  KEYBOARDVERSION|KMW_EMBEDCSS|KMW_EMBEDJS|KMW_HELPFILE|KMW_HELPTEXT|KMW_RTL|
  LANGUAGE|LAYOUTFILE|MESSAGE|MNEMONICLAYOUT|NAME|TARGETS|VERSION|
  VISUALKEYBOARD|WINDOWSLANGUAGES|CAPSALWAYSOFF|CAPSONONLY|SHIFTFREESCAPS
casedkeysStoreAssign: casedkeysStore prePadText+
casedkeysStore: STORE LEFT_BR WHITESPACE? AMPERSAND CASEDKEYS WHITESPACE? RIGHT_BR
hotkeyStoreAssign: hotkeyStore padding virtualKey
hotkeyStore: STORE LEFT_BR WHITESPACE? AMPERSAND HOTKEY WHITESPACE? RIGHT_BR
variableStoreAssign: variableStore prePadText+
variableStore: STORE bracketedStoreName
bracketedStoreName: LEFT_BR WHITESPACE? variableStoreName WHITESPACE? RIGHT_BR
variableStoreName: PARAMETER
systemStoreName: stringSystemStoreName|CASEDKEYS|HOTKEY|LAYER|NEWLAYER|OLDLAYER
permittedKeyword: NEWCONTEXT|POSTKEYSTROKE
setStore: SET LEFT_BR WHITESPACE? variableStoreName WHITESPACE EQUAL
  WHITESPACE STRING WHITESPACE? RIGHT_BR
setLayer: SET LEFT_BR WHITESPACE? AMPERSAND LAYER WHITESPACE EQUAL
  WHITESPACE STRING WHITESPACE? RIGHT_BR
resetStore: RESET bracketedStoreName
capsAlwaysOff: CAPS whitespace ALWAYS whitespace OFF
capsOnOnly: CAPS whitespace ON whitespace ONLY
shiftFreesCaps: SHIFT whitespace FREES whitespace CAPS
