kmnTree: line*
line: contentLine|blankLine
contentLine: WHITESPACE? content WHITESPACE? COMMENT? NEWLINE
blankLine: WHITESPACE? COMMENT? NEWLINE
padding: continuationNewline|WHITESPACE
continuationNewline: WHITESPACE? CONTINUATION WHITESPACE? NEWLINE WHITESPACE?
content: stringSystemStoreAssign|casedkeysStoreAssign|hotkeyStoreAssign|
  variableStoreAssign|ruleBlock
stringSystemStoreAssign: stringSystemStore padding STRING
stringSystemStore: STORE LEFT_BR WHITESPACE? AMPHASAND stringSystemStoreName WHITESPACE? RIGHT_BR
stringSystemStoreName: BITMAP|COPYRIGHT|DISPLAYMAP|ETHNOLOGUECODE|INCLUDECODES|
  KEYBOARDVERSION|KMW_EMBEDCSS|KMW_EMBEDJS|KMW_HELPFILE|KMW_HELPTEXT|KMW_RTL|
  LANGUAGE|LAYOUTFILE|MESSAGE|MNEMONICLAYOUT|NAME|TARGETS|VERSION|
  VISUALKEYBOARD|WINDOWSLANGUAGES
casedkeysStoreAssign: casedkeysStore paddedText+
casedkeysStore: STORE LEFT_BR WHITESPACE? AMPHASAND CASEDKEYS WHITESPACE? RIGHT_BR
hotkeyStoreAssign: hotkeyStore padding virtualKey
hotkeyStore: STORE LEFT_BR WHITESPACE? AMPHASAND HOTKEY WHITESPACE? RIGHT_BR
variableStoreAssign: variableStore paddedText+
paddedText: padding text
text: textRange|simpleText|outsStatement
simpleText: STRING|virtualKey|U_CHAR
textRange: simpleText rangeEnd+
rangeEnd: WHITESPACE? RANGE WHITESPACE? simpleText
virtualKey: LEFT_SQ WHITESPACE? spacedShiftCode* KEY_CODE WHITESPACE? RIGHT_SQ
spacedShiftCode: SHIFT_CODE WHITESPACE
variableStore: STORE bracketedStoreName
bracketedStoreName: LEFT_BR WHITESPACE? variableStoreName WHITESPACE? RIGHT_BR
variableStoreName: PARAMETER
outsStatement: OUTS bracketedStoreName
ruleBlock: beginBlock|groupBlock|productionBlock
beginBlock: beginStatement spacedChevron useStatement
beginStatement: BEGIN spacedEntryPoint?
spacedEntryPoint: WHITESPACE entryPoint
entryPoint: UNICODE|NEWCONTEXT|POSTKEYSTROKE|ANSI
spacedChevron: WHITESPACE CHEVRON WHITESPACE
useStatement: USE bracketedGroupName
bracketedGroupName: LEFT_BR WHITESPACE? groupNameOrKeyword WHITESPACE? RIGHT_BR
groupNameOrKeyword: groupName|permittedKeyword
permittedKeyword: NEWCONTEXT|POSTKEYSTROKE
groupName: PARAMETER
groupBlock: groupStatement spacedGroupQualifier?
groupStatement: GROUP bracketedGroupName
spacedGroupQualifier: WHITESPACE groupQualifier
groupQualifier: usingKeys|READONLY
usingKeys: USING WHITESPACE KEYS
productionBlock: lhsBlock spacedChevron rhsBlock
lhsBlock: MATCH|NOMATCH|inputBlock|ifLikeBlock
inputBlock: ifLikeBlock? padding inputCharacter
inputCharacter: anyStatement
anyStatement: ANY bracketedStoreName
ifLikeBlock: ifLikeStatement paddedIfLikeStatement*
paddedIfLikeStatement: padding IfLikeStatement
ifLikeStatement: ifStatement|layerStatement|platformStatement|baselayoutStatement
ifStatement: ifStoreStoreStatement|ifStoreStringStatement|ifSystemStoreStoreStatement
  ifSystemStoreStringStatement
ifStoreStoreStatement: IF LEFT_BR WHITESPACE? variableStoreName WHITESPACE
  comparison WHITESPACE variableStoreName WHITESPACE? RIGHT_BR
ifStoreStringStatement: IF LEFT_BR WHITESPACE? variableStoreName WHITESPACE
  comparison WHITESPACE STRING WHITESPACE? RIGHT_BR
ifSystemStoreStoreStatement: IF LEFT_BR WHITESPACE? AMPHASAND systemStoreName
  WHITESPACE comparison WHITESPACE variableStoreName WHITESPACE? RIGHT_BR
ifSystemStoreStringStatement: IF LEFT_BR WHITESPACE? AMPHASAND systemStoreName
  WHITESPACE comparison WHITESPACE STRING WHITESPACE? RIGHT_BR
ifSystemStoreName: PLATFORM|LAYER|BASELAYOUT|NEWLAYER|OLDLAYER
comparison:EQUAL|NOT_EQUAL
systemStoreName: stringSystemStoreName|CASEDKEYS|HOTKEY|LAYER|NEWLAYER|OLDLAYER
layerStatement: LAYER bracketedString
platformStatement: PLATFORM bracketedString
baselayoutStatement: BASELAYOUT bracketedString
bracketedString: LEFT_BR WHITESPACE? STRING WHITESPACE? RIGHT_BR
rhsBlock: contextOutputBlock|CONTEXT
contextOutputBlock: paddedContext? outputBlock
paddedContext: CONTEXT padding
outputBlock: outputStatement paddedOutputStatement*
paddedOutputStatement: padding outputStatement
outputStatement: useStatement|layerStatement

indexStatement: INDEX LEFT_BR WHITESPACE? variableStoreName spacedComma offset WHITESPACE? RIGHT_BR
spacedComma: WHITESPACE? COMMA WHITESPACE?