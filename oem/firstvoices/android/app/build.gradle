plugins {
    id 'com.android.application'
    id 'io.sentry.android.gradle'
    id 'com.github.triplet.play' version '3.9.1' apply false
}

ext.rootPath = '../../../../android'
apply from: "$rootPath/version.gradle"

android {
    compileSdk 34
    namespace="com.firstvoices.keyboards"

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        applicationId "com.firstvoices.keyboards"
        minSdkVersion 21
        targetSdkVersion 35

        // KEYMAN_VERSION_CODE and KEYMAN_VERSION_NAME from version.gradle
        versionCode KEYMAN_VERSION_CODE as Integer
        versionName KEYMAN_VERSION_NAME
        buildConfigField "String", "KEYMAN_VERSION_ENVIRONMENT", "\""+KEYMAN_VERSION_ENVIRONMENT+"\""
        buildConfigField "String", "KEYMAN_VERSION_GIT_TAG", "\""+KEYMAN_VERSION_GIT_TAG+"\""
    }

    String env_release_store_file = System.getenv("oem_firstvoices_release_store_file")
    String env_release_store_password = System.getenv("oem_firstvoices_release_store_password")
    String env_release_key_alias = System.getenv("oem_firstvoices_release_key_alias")
    String env_release_key_password = System.getenv("oem_firstvoices_release_key_password")
    if (env_release_store_file != null && file(env_release_store_file).exists()) {
        signingConfigs {
            release {
                println "Using signing from environment"
                storeFile file(String.valueOf(env_release_store_file))
                storePassword env_release_store_password
                keyAlias env_release_key_alias
                keyPassword env_release_key_password
            }
        }
    }

    buildTypes {
        /*debug {
            applicationIdSuffix ".debug"
        }*/
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (env_release_store_file != null && file(env_release_store_file).exists()) {
                signingConfig signingConfigs.release
            }
        }
    }
    productFlavors {
    }

    applicationVariants.all { variant ->
        // Adjust output name to "firstvoices-${KEYMAN_VERSION_MD}.apk"
        variant.outputs.all {
            outputFileName = "firstvoices-" + KEYMAN_VERSION_MD + ".apk"
        }
    }
    lintOptions {
        disable 'MissingTranslation'
        lintConfig file("lint.xml")
    }
}

// how to configure the sentry android gradle plugin
task publishSentry {
    doLast {
        println 'Publishing FirstVoices symbols to Sentry'
        sentry {
            // Disables or enables the automatic configuration of Native symbols
            uploadNativeSymbols = true

            // Does or doesn't include the source code of native code for Sentry
            includeNativeSources = true
        }
    }
}

String env_keys_json_file = System.getenv("oem_firstvoices_keys_json_file")
if (env_keys_json_file != null && file(env_keys_json_file).exists()) {
    apply plugin: 'com.github.triplet.play'

    play {
        serviceAccountCredentials = file(String.valueOf(env_keys_json_file))

        // Deactivate lower conflicting version APKs
        resolutionStrategy.set(com.github.triplet.gradle.androidpublisher.ResolutionStrategy.IGNORE)
        String tier = new File('../../../TIER.md').getText('UTF-8').trim();
        switch (tier) {
            case 'beta':
                track.set("beta")
                println "TIER set to beta"
                break

            case 'stable':
                track.set("production")
                println "TIER set to production"
                break

            default:
                track.set("alpha")
                println "TIER set to alpha"
        }
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
    google()
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    api(name: 'keyman-engine', ext: 'aar')
    implementation 'io.sentry:sentry-android:7.8.0'
    implementation 'androidx.preference:preference:1.2.1'
}

apply plugin: 'com.android.application'
