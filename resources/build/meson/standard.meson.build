#
# Shared configuration for all meson-based builds
#
# This file has the master location /resources/build/meson/standard.meson.build,
# and is copied into each project's <project>/resources/meson.build in the
# configure step, so that it can be referenced by meson directly.
#
# Standard code block for copying this file into a meson project:
#
#        # Import our standard compiler defines; this is copied from
#        # /resources/build/meson/standard.meson.build by build.sh, because meson doesn't
#        # allow us to reference a file outside its root
#        mkdir -p "$THIS_SCRIPT_PATH/resources"
#        cp "$KEYMAN_ROOT/resources/build/meson/standard.meson.build" "$THIS_SCRIPT_PATH/resources/meson.build"
#        # Write our tagged version environment variable into a file so meson can read it
#        echo "$VERSION_WITH_TAG" > "$THIS_SCRIPT_PATH/resources/VERSION_WITH_TAG.md"
#
# Where possible, we want to use these flags everywhere
#

cpp_compiler = meson.get_compiler('cpp')
c_compiler = meson.get_compiler('c')

# Once we can assume meson 0.60 we can delete this
# (https://mesonbuild.com/Release-notes-for-0-60-0.html#msvc-compiler-now-assumes-utf8-source-code-by-default)
if cpp_compiler.get_id() == 'msvc'
  add_global_arguments('/source-charset:utf-8', language: ['c', 'cpp'])
  add_global_arguments('/execution-charset:utf-8', language: ['c', 'cpp'])
endif

#
# Standard informational messages for our builds
#

message('meson.project_version(): ' + meson.project_version())
message('host_machine.system(): ' + host_machine.system())
message('compiler.get_id(): ' + cpp_compiler.get_id())

#
# Import `VERSION_WITH_TAG` which we can use where appropriate instead of
# meson.project_version() which is only major.minor.patch.
#

fs = import('fs')
if fs.exists('VERSION_WITH_TAG.md')
  VERSION_WITH_TAG = fs.read('VERSION_WITH_TAG.md').strip()
  message('VERSION_WITH_TAG: ' + VERSION_WITH_TAG)
endif

#
# Standard compiler flags for all platforms
#

warns = []
flags = []
links = []
defns = []

if cpp_compiler.get_id() == 'gcc' or cpp_compiler.get_id() == 'clang'
  warns += [
     '-Wctor-dtor-privacy',
     '-Wdouble-promotion',
     '-Wendif-labels',
     '-Wno-unknown-pragmas',
     '-Wno-missing-field-initializers',
     '-Wnon-virtual-dtor',
     '-Wshadow'
  ]
  flags += [
    '-fvisibility=hidden',
    '-fvisibility-inlines-hidden'
  ]

  if cpp_compiler.get_id() == 'clang'
    warns += [
      '-Wimplicit-fallthrough',
      '-Wno-double-promotion',
      '-Wshorten-64-to-32'
    ]
  endif

  if host_machine.system() == 'darwin'
    # TODO: this seems to be inverting above options, is this necessary, or can
    #       we resolve another way?
    warns += [
      '-Wno-ctor-dtor-privacy',
      '-Wno-non-virtual-dtor'
    ]
  endif
endif

if cpp_compiler.get_id() == 'msvc'
  defns += [
    '-D_SCL_SECURE_NO_WARNINGS',
    '-D_CRT_SECURE_NO_WARNINGS'
  ]
endif

if cpp_compiler.get_id() == 'emscripten'
  if get_option('buildtype') == 'debug'
    # Enable DWARF symbols for debug builds
    flags += ['-g']
    links += ['-g']
  else
    flags += ['-O2']
    links += ['-O2']
  endif
endif

# Disable assertions on release builds
if get_option('buildtype') != 'debug'
  defns += [
    '-DNDEBUG'
  ]
endif
