c European Latin keyboard for Keyman Desktop 7.0 and KeymanWeb 1.0
c 
c Copyright 2006-2007 Tavultesoft Pty Ltd
c 
c If you make changes to this keyboard file, please consider sharing them at:
c http://www.tavultesoft.com/forums/category.php?ForumSourceID=1&SourceID=348

VERSION 7.0
NAME "European Latin"
store(&BITMAP) 'latin.ico'
store(&COPYRIGHT) 'Copyright (C) 2006-2007 Tavultesoft Pty Ltd'

c store(&mnemoniclayout) '1'

store(&KMW_HelpFile) 'european.html'
store(&KMW_EmbedJS) 'european_js_2.txt'

begin Unicode > use(Main)

group( Main ) using keys

store(controls)  "'`^:~@$\-,*%." '"'
              
c -
store(macronO) 'ĀāĐđĒēĦħĪīŁłŌōŦŧŪū'
store(macronK) 'AaDdEeHhIiLlOoTtUu'

c '
store(acuteO) 'áéíóúýćĺńŕśźÁÉÍÓÚÝĆĹŃŔŚŹ'
store(acuteK) 'aeiouyclnrszAEIOUYCLNRSZ'

c *
store(breveO) 'ăĂĕĔğĞĭĬŏŎŭŬ'
store(breveK) 'aAeEgGiIoOuU'
   
c %
store(caronO) 'čČďĎěĚľĽňŇřŘšŠťŤžŽ'
store(caronK) 'cCdDeElLnNrRsStTzZ'

c .
store(dottedO) 'ḃḂċĊḋḊėĖḟḞġĠḣḢıİŀĿṁṀṅṄṗṖṙṘṡṠṫṪẇẆẋẊẏẎżŻ'
store(dottedK) 'bBcCdDeEfFgGhHiIlLmMnNpPrRsStTwWxXyYzZ'

c "
store(doubleacuteO) 'őŐűŰ'
store(doubleacuteK) 'oOuU'


c ,
store(cedillaO) 'ąĄçÇģĢęĘįĮķĶļĻņŅŗŖşŞţŢųŲ'
store(cedillaK) 'aAcCgGeEiIkKlLnNrRsStTuU'

c `
store(graveO)    'àèìòùÀÈÌÒÙ'
store(graveK)    'aeiouAEIOU'
                
c ^
store(circumO)   'âĉêĝĥîĵôûŝŵŷÂĈÊĜĤÎĴÔÛŜŴŶ'
store(circumK)   'aceghijouswyACEGHIJOUSWY'
   
c :
store(dieresisO) 'äëïöüÿÄËÏÖÜŸ'
store(dieresisK) 'aeiouyAEIOUY'
   
c ~
store(tildeO) 'ãÃĩĨñÑõÕũŨ'
store(tildeK) 'aAiInNoOuU'               

c @
store(ringO) 'åÅůŮ'
store(ringK) 'aAuU'

c \
store(specialO) 'ĸŉſÐðØø¡¦§¨©ª¬­®¯°±²³´µ¶·¸¹º¿×÷†‡‰–—•'
store(specialK) "knsDdOo!|S:cf~-r_*+23'uP.,1m?x/tT%NM>"

c $
store(currencyO) '¢£¤¥₣₤₧€'
store(currencyK) 'cpCyFlPe'

"'" dk(1) + any(acuteK) > index(acuteO, 3)
"`" dk(1) + any(graveK) > index(graveO, 3)
"^" dk(1) + any(circumK) > index(circumO, 3)
':' dk(1) + any(dieresisK) > index(dieresisO, 3)
'~' dk(1) + any(tildeK) > index(tildeO, 3)
'@' dk(1) + any(ringK) > index(ringO, 3)
'$' dk(1) + any(currencyK) > index(currencyO, 3)
'-' dk(1) + any(macronK) > index(macronO, 3)
'*' dk(1) + any(breveK) > index(breveO, 3)
'%' dk(1) + any(caronK) > index(caronO, 3)
'.' dk(1) + any(dottedK) > index(dottedO, 3)
'"' dk(1) + any(doubleacuteK) > index(doubleacuteO, 3)
',' dk(1) + any(cedillaK) > index(cedillaO, 3)

'·.' + '.' > '…'

'\' dk(1) + any(specialK) > index(specialO, 3)
c '\' dk(1) 'A' + 'E' > 'Æ'
c '\' dk(1) 'a' + 'e' > 'æ'
'A\' dk(1) + 'E' > 'Æ'
'a\' dk(1) + 'e' > 'æ'
'T\' dk(1) + 'H' > 'Þ'
't\' dk(1) + 'h' > 'þ'
's\' dk(1) + 's' > 'ß'
'o\' dk(1) + 'e' > 'œ'
'O\' dk(1) + 'E' > 'Œ'
'i\' dk(1) + 'j' > 'ĳ'
'I\' dk(1) + 'J' > 'Ĳ'
'n\' dk(1) + 'g' > 'ŋ'
'N\' dk(1) + 'G' > 'Ŋ'
'f\' dk(1) + 'i' > 'ﬁ'
'f\' dk(1) + 'l' > 'ﬂ'
'T\' dk(1) + 'M' > '™'
't\' dk(1) + 'm' > '™'

'¹/' + '4' > '¼'
'¹/' + '2' > '½'
'³/' + '4' > '¾'
'¹/' + '3' > '⅓'
'²/' + '3' > '⅔'
'¹/' + '8' > '⅛'
'³/' + '8' > '⅜'
'\' dk(1) '5/' + '8' > '⅝'
'\' dk(1) '7/' + '8' > '⅞'

c angled quotes
"<" + "<" > '«'
">" + ">" > '»'
'«' + '<' > '<<' dk(2)
'»' + '>' > '>>' dk(2)
"<" dk(2) + "<" > '<<' dk(2)
">" dk(2) + ">" > '>>' dk(2)

c Rules for the accent character itself (type it twice)

"'" dk(1) + "'" > "'"  c Quote
"`" dk(1) + "`" > "`"  c Backquote
"^" dk(1) + "^" > "^"  c Caret
':' dk(1) + ':' > ':'  c Double-quote
'~' dk(1) + '~' > '~'  c tilde
'@' dk(1) + '@' > '@'  c ring
'$' dk(1) + '$' > '$'  c dollar
'\' dk(1) + '\' > '\'  c backslash
'-' dk(1) + '-' > '-'  c hyphen
',' dk(1) + ',' > ','  c comma
'*' dk(1) + '*' > '*'  c backslash
'%' dk(1) + '%' > '%'  c backslash
'.' dk(1) + '.' > '.'  c backslash
'"' dk(1) + '"' > '"'  c backslash

+ any(controls) > index(controls, 1) dk(1)

c End of keyboard
