plugins {
    id 'com.android.library'
    id 'name.remal.default-plugins'
}

ext.rootPath = '../../'
apply from: "$rootPath/version.gradle"

android {
    compileSdk 34
    namespace "com.keyman.engine"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34

        // VERSION_CODE and VERSION_NAME from version.gradle but Gradle removes them for libraries
        buildConfigField "String", "KEYMAN_ENGINE_VERSION_NAME", "\""+VERSION_NAME+"\""
        buildConfigField "String", "VERSION_ENVIRONMENT", "\""+VERSION_ENVIRONMENT+"\""

        // Sepcify library filename keyman-engine-$CONFIG.aar
        setProperty("archivesBaseName", "keyman-engine")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    // TODO: Remove ResourceType when SDK > 17
    lintOptions {
        disable 'ImpliedQuantity', 'MissingQuantity', 'MissingTranslation', 'ResourceType'
        lintConfig file("lint.xml")
    }

    testOptions {
        unitTests {
            // Allows use of a simulated Android API for tests.  (Thanks, roboelectric!)
            includeAndroidResources = true
        }
        unitTests.all {
            testLogging {
                // May also add "passed", "skipped".
                events "failed"//, "standardOut", "standardError" if we want them to show in builds.
                outputs.upToDateWhen {false}
                //showStandardStreams = true // If we want test console log output to show during builds.
            }
            systemProperty 'kmeaTestMode', 'true'
            workingDir = "../" // Defaults to the `app` subdirectory, which is different from Android Studio's default.
        }
    }
}

dependencies {
    // 1.6.0-rc01 needed to resolve https://issuetracker.google.com/issues/238425626
    implementation 'androidx.appcompat:appcompat:1.6.1'
    // material:1.7.0 will need Gradle plugin 7.1.0+
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'commons-io:commons-io:2.13.0'
    implementation 'io.sentry:sentry-android:7.8.0'
    implementation 'androidx.preference:preference:1.2.1'

    // Robolectric
    testImplementation 'androidx.test:core:1.5.0'
    testImplementation 'androidx.test.ext:junit:1.1.5'
    testImplementation 'org.robolectric:robolectric:4.10.3'

    // Generate QR Codes
    implementation ('com.github.kenglxn.QRGen:android:3.0.1') {
        transitive = true
    }
}
//Show deprecation compiler warnings
/*
allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation"
    }
}*/
